plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'java-gradle-plugin'
//    id "com.gradle.plugin-publish" version '1.1.0'
}


java {
    sourceCompatibility = '1.8'
}

version = pluginVersion
group = pluginGroup

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly( gradleApi() )
}
gradlePlugin {
    website = 'https://github.com/WebOSCrud/web-os-gradle-plugin'
    vcsUrl = 'https://github.com/WebOSCrud/web-os-gradle-plugin.git'
    plugins {
        greetingsPlugin {
            id = "cn.donting.web.os.gradle-plugin"
            displayName ="web os wap dev"
            description ="web os wap dev"
            implementationClass = 'cn.donting.web.os.gradle.plugin.TaskRegister'
            tags.set(['web-os'])
        }
    }
}


// 打包sourcesJar任务
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

// 打包javadocJar任务
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}


publishing {
    repositories {
        maven {
            File mv = new File(getProject().getRootDir().getPath() + File.separator + "maven.properties")
            if(!mv.exists()){
                return
            }
            Properties properties = new Properties()
            properties.load(new FileInputStream(mv))
            def userName = properties.getProperty("username")
            def passWord = properties.getProperty("password")
            def releasesRepoUrl = "https://oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username userName
                password passWord
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = pluginGroup
            artifactId = "web-os-gradle-plugin"
            version = pluginVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'web-os-gradle-plugin'
                description = 'web-os-gradle-plugin'
                url = 'https://github.com/WebOSCrud/web-os-gradle-plugin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'donting'
                        name = 'donting'
                        email = '596643891@qq.com'
                    }
                }

            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

